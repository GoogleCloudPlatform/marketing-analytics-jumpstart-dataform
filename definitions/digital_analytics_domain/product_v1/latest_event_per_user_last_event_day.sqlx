/*
* Copyright 2025 Google LLC
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     https://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
config {
    type: "view",
    schema: functions.productSchema("ga4"),
    description: "Latest event per user from the last event day in event table",
    tags: ['ga4']
}

js {
    const event_type_column = 'event_type';
    const timestamp_column = 'event_timestamp';
    const event_date_column = 'event_date';
    const group_by_columns = [
        'user_id',
        'user_pseudo_id',
    ];
    const columns = [
        'event_id',
        'event_name',
        event_type_column,
        timestamp_column,
        'ga_session_id',
        'stream_id'
    ];
    const table_alias = 't';
}

SELECT
  ${group_by_columns},
  ${sql.selectMultiColumnFromFirstEntry(table_alias, timestamp_column, 'DESC', columns)}
FROM
  ${ref({schema: functions.productSchema("ga4"), name: "event"})} AS ${table_alias}
WHERE
  ${table_alias}.${event_date_column} = (
    SELECT MAX(${event_date_column})
    FROM ${ref({schema: functions.productSchema("ga4"), name: "event"})}
  )
  AND ${table_alias}.${event_type_column} IN ('tagged')
GROUP BY
  ${group_by_columns}
