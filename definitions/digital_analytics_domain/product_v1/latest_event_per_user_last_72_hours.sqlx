/*
* Copyright 2023 Google LLC
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     https://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
config {
  type: "view",
  schema: functions.productSchema("ga4"),
  description: "Latest event per user within the last 72 hours window",
  tags: ['ga4']
}

js {
  const columns = [
    'user_pseudo_id',
    'user_id',
    'event_id',
    'event_name',
    'event_type',
    'event_timestamp'
  ]
}

SELECT
${columns}
FROM (
  SELECT
    ${columns},
    ROW_NUMBER() OVER (PARTITION BY user_pseudo_id ORDER BY event_timestamp DESC) AS rn
  FROM
    ${ref({schema: functions.baseSchema("ga4"), name: "event"})}
  WHERE
    event_timestamp > TIMESTAMP_SUB(CURRENT_TIMESTAMP(), INTERVAL 3 DAY)
    AND event_type IN ('tagged') )
WHERE
  rn = 1
