/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
js {
  function intEventParam(parameter) {
    return '(SELECT ep.value.int_value FROM UNNEST(event_params) ep WHERE ep.key = "'
    + parameter + '" LIMIT 1)';
  }
  function stringEventParam(parameter) {
    return '(SELECT ep.value.string_value FROM UNNEST(event_params) ep WHERE ep.key = "'
    + parameter + '" LIMIT 1)';
  }
  function stripNotSet(expression, columnName) {
    if(typeof columnName === 'undefined') {
      columnName = expression;
    }
    return 'CASE WHEN ' + expression + '="(not set)" THEN "" ELSE ' + expression + ' END as ' + columnName
  }
  function notNullString(column, columnName) {
    return 'COALESCE(' + column + ', "") as ' + columnName;
  }
}

config { 
  type: "view",
  schema: functions.baseSchema("ga4"),
  columns: docs.ga4_events,
  description: "View into GA4 exported tables. Not be used outside of the Dataform transformation jobs.",
  tags: ['ga4']
}

SELECT
  --traffic_source dimension
  ${notNullString('traffic_source_medium', 'traffic_source_medium')},
  ${notNullString('traffic_source_name', 'traffic_source_name')},
  ${notNullString('traffic_source', 'traffic_source')},
  ${notNullString('collected_traffic_source_manual_campaign_id', 'collected_traffic_source_manual_campaign_id')},
  ${notNullString('event_params_page_referrer','collected_traffic_source_page_referrer')},
  ${notNullString('CASE WHEN collected_traffic_source_manual_campaign_name IS NULL THEN event_params_campaign ELSE collected_traffic_source_manual_campaign_name END','collected_traffic_source_manual_campaign_name')},
  ${notNullString('CASE WHEN collected_traffic_source_manual_source IS NULL THEN event_params_source ELSE collected_traffic_source_manual_source END','collected_traffic_source_manual_source')},
  ${notNullString('CASE WHEN collected_traffic_source_manual_medium IS NULL THEN event_params_medium ELSE collected_traffic_source_manual_medium END','collected_traffic_source_manual_medium')},
  ${notNullString('collected_traffic_source_manual_term', 'collected_traffic_source_manual_term')},
  ${notNullString('collected_traffic_source_manual_content', 'collected_traffic_source_manual_content')},
  ${notNullString('CASE WHEN collected_traffic_source_gclid IS NULL THEN event_params_gclid ELSE collected_traffic_source_gclid END','collected_traffic_source_gclid')},
  ${notNullString('event_params_dclid','collected_traffic_source_dclid')},
  ${notNullString('collected_traffic_source_gmp_id', 'collected_traffic_source_gmp_id')},
  ${notNullString('collected_traffic_source_merchant_center_id','collected_traffic_source_merchant_center_id')},
  CASE WHEN event_params_dclid IS NOT NULL THEN "gmp" WHEN event_params_gclid IS NOT NULL THEN "ads" ELSE "" END as advertiser_platform,
  --geo dimension
  ${stripNotSet('continent')}, 
  ${stripNotSet('subcontinent')},
  ${stripNotSet('country')},
  ${stripNotSet('region')},
  ${stripNotSet('city')},
  ${stripNotSet('metro')},
  --device dimension
  ${notNullString('device_category','device_category')},
  ${notNullString('device_mobile_brand_name','device_mobile_brand_name')},
  ${notNullString('device_mobile_model_name','device_mobile_model_name')},
  ${notNullString('device_model','device_model')},
  ${notNullString('device_os','device_os')},
  ${notNullString('device_os_version','device_os_version')},
  ${notNullString('device_web_browser','device_web_browser')},
  ${notNullString('device_web_browser_version','device_web_browser_version')},
  COALESCE(SPLIT(device_web_browser_version, '.')[OFFSET(0)], "") as normalized_device_web_browser_version,
  COALESCE(app_store, "") as app_store,
  COALESCE(app_version, "") as app_version,
  ${notNullString('language', 'language')},
  COALESCE(left(language, 2), "") as normalized_language,
  COALESCE(device_advertising_id, "") as device_advertising_id,
  --event fact
  TIMESTAMP_MICROS(event_timestamp) as event_timestamp,
  event_name,
  platform,
  stream_id,
  --user dimension
  user_pseudo_id,
  user_id,
  TIMESTAMP_MICROS(user_first_touch_timestamp) as user_first_touch_timestamp,
  is_active_user,
  --session fact
  ga_session_id,
  ga_session_number,
  COALESCE(CASE WHEN session_engaged_during_event IS NULL THEN CAST(event_engaged_session_event AS BOOL) ELSE CAST(session_engaged_during_event AS BOOL) END, FALSE) as session_engaged_during_event,
  engagement_time_msec,
  -- page dimension
  page_location, 
  page_title,
  page_referrer,
  percent_scrolled,
  outbound_click_url,
  CASE WHEN page_location IS NULL THEN NULL ELSE REGEXP_EXTRACT(page_location,".*[?&]gclid=([^&#]+)") END gclid, 
  --ecommerce fact
  ecommerce,
  user_ltv_revenue,
  user_ltv_currency,
  -- items fact
  ${sql.selectFieldsFromRepeatedRecord('items', ga4.eventsItemsSelectedColumns, ga4.eventsItemsSafeCastColumns)},
  STRUCT (
      privacy_info.analytics_storage = 'No' as analytics_excluded,
      privacy_info.ads_storage = 'No' as ads_excluded,
      privacy_info.uses_transient_token = 'Yes' as uses_transient_token
  ) as privacy_info,
  table_suffix
FROM (
SELECT
    LOWER(traffic_source.medium) as traffic_source_medium, 
    LOWER(traffic_source.name) as traffic_source_name, 
    LOWER(traffic_source.source) as traffic_source, 
    LOWER(collected_traffic_source.manual_campaign_id) as collected_traffic_source_manual_campaign_id,
    LOWER(collected_traffic_source.manual_campaign_name) as collected_traffic_source_manual_campaign_name,
    LOWER(collected_traffic_source.manual_source) as collected_traffic_source_manual_source,
    LOWER(collected_traffic_source.manual_medium) as collected_traffic_source_manual_medium,
    LOWER(collected_traffic_source.manual_term) as collected_traffic_source_manual_term,
    LOWER(collected_traffic_source.manual_content) as collected_traffic_source_manual_content,
    LOWER(collected_traffic_source.gclid) as collected_traffic_source_gclid,
    LOWER(collected_traffic_source.dclid) as collected_traffic_source_gmp_id,
    LOWER(collected_traffic_source.srsltid) as collected_traffic_source_merchant_center_id,
    geo.continent as continent,
    geo.sub_continent as subcontinent,
    geo.country as country,
    geo.region as region,
    geo.city as city,
    geo.metro as metro,
    device.category as device_category,
    device.mobile_brand_name as device_mobile_brand_name,
    device.mobile_model_name as device_mobile_model_name,
    device.mobile_marketing_name as device_model,
    device.operating_system as device_os,
    device.operating_system_version as device_os_version,
    device.web_info.browser as device_web_browser,
    device.web_info.browser_version as device_web_browser_version,
    app_info.install_store as app_store,
    app_info.version as app_version,
    SAFE_CAST(event_timestamp AS INT64) as event_timestamp,
    event_name,
    platform,
    stream_id,
    user_id,
    user_pseudo_id,
    device.language as language,
    device.advertising_id as device_advertising_id,
    STRUCT (
      ${stripNotSet('ecommerce.transaction_id', 'transaction_id')},
      SAFE_CAST(ecommerce.purchase_revenue_in_usd AS NUMERIC) as purchase_revenue_in_usd,
      ecommerce.total_item_quantity as total_item_quantity,
      ecommerce.refund_value_in_usd as refund_value_in_usd
    ) ecommerce,
    ${intEventParam("ga_session_id")} as ga_session_id,
    ${intEventParam("ga_session_number")} as ga_session_number,
    (SELECT COALESCE(ep.value.int_value, SAFE_CAST(ep.value.string_value AS INT64)) FROM UNNEST(event_params) ep WHERE ep.key = "session_engaged" LIMIT 1) as session_engaged_during_event,
    ${intEventParam("engagement_time_msec")} as engagement_time_msec,
    ${stringEventParam("page_location")} as page_location,
    ${stringEventParam("page_title")} as page_title,
    ${stringEventParam("page_referrer")} as page_referrer,
    ${intEventParam("percent_scrolled")} as percent_scrolled,
    ${stringEventParam("link_url")} as outbound_click_url,
    ${stringEventParam("medium")} as event_params_medium,
    ${stringEventParam("source")} as event_params_source,
    ${intEventParam("engaged_session_event")} as event_engaged_session_event,
    ${stringEventParam("campaign")} as event_params_campaign,
    ${stringEventParam("page_referrer")} as event_params_page_referrer,
    ${stringEventParam("dclid")} as event_params_dclid,
    ${stringEventParam("gclid")} as event_params_gclid,
    SAFE_CAST(user_ltv.revenue AS NUMERIC) as user_ltv_revenue,
    user_ltv.currency as user_ltv_currency,
    SAFE_CAST(user_first_touch_timestamp AS INT64) as user_first_touch_timestamp,
    items,
    privacy_info,
    is_active_user,
    _TABLE_SUFFIX as table_suffix
FROM ${ref('events' + dataform.projectConfig.vars.ga4_export_table_suffix)}
  -- Exclude extra day exports 
  WHERE NOT STARTS_WITH(_TABLE_SUFFIX, "intraday")
)