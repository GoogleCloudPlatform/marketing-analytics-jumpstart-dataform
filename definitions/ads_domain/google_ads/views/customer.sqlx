/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config { 
  type: "view",
  schema: functions.baseSchema("ads"),
  tags: ["ads"]
}
js {
  const columns =
    `customer_id,
    customer_descriptive_name,
    customer_currency_code,
    customer_time_zone`;

  const union = functions.buildViewUnion(columns, "ads", "p_ads_Customer");
}
SELECT DISTINCT
  "GOOGLE_ADS" AS ad_system,
  CAST(customer_id AS STRING) AS account_id,
  customer_descriptive_name AS account_name,
  customer_currency_code AS currency,
  customer_time_zone AS timezone
FROM (
${union}
)