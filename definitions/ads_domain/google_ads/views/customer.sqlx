/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config { 
  type: "view",
  schema: functions.baseSchema("ads"),
  tags: ["ads"]
}
js {
  const columns =
    `customer_id,
    customer_auto_tagging_enabled,
    customer_currency_code,
    customer_descriptive_name,
    customer_manager,
    customer_test_account,
    customer_time_zone`;

  const union = functions.buildAdsViewUnion(columns, "ads_Customer");
}
SELECT DISTINCT
  "GOOGLE_ADS" AS ad_system,
  CAST(customer_id AS STRING) AS account_id,
  customer_auto_tagging_enabled AS auto_tagging_enabled,
  customer_descriptive_name AS account_name,
  customer_manager AS is_manager,
  customer_test_account AS is_test_account,
  customer_currency_code AS currency,
  customer_time_zone AS timezone
FROM (
${union}
)
QUALIFY ROW_NUMBER() OVER (PARTITION BY CAST(customer_id AS STRING)) = 1
