/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config { 
  type: "table",
  schema: functions.baseSchema("ads"),
  dependencies: [ "update_dim_ads"],
  tags: ["ads"]
}

WITH landing AS (
  SELECT
    dim_ads.ad_id as ad_id,
    l.date_id as date_id,
    l.device as device,
    l.conversion_tracker_id as conversion_tracker_id,
    l.ad_conversion_type as ad_conversion_type,
    l.ad_conversion_name as ad_conversion_name,
    l.value as value,
    l.quantity as quantity
  FROM ${ref("ad_conversion_stats")} l
  LEFT JOIN (
    SELECT DISTINCT
      ad_system,
      ad_id, 
      account_id,
      campaign_id,
      adgroup_id,
      creative_id
    FROM ${ref("dim_ads")}
    WHERE current_flag = TRUE
  ) dim_ads 
  USING (ad_system, account_id, campaign_id, adgroup_id, creative_id)
),
warehouse AS (
  SELECT 
    ad_conversion_id,
    date_id,
    ad_id,
    device,
    conversion_tracker_id,
    ad_conversion_type,
    ad_conversion_name,
    value,
    quantity
  FROM ${ref("fact_ad_conversion_daily")}
)
SELECT 
  l, w,
  CASE
    WHEN w.ad_conversion_id IS NULL THEN "APPEND"
    WHEN NOT (
      l.ad_conversion_type = w.ad_conversion_type AND
      l.ad_conversion_name = w.ad_conversion_name AND 
      l.value = w.value AND
      l.quantity = w.value
    )
    THEN "MODIFY"
    ELSE "NO_ACTION"
  END disposition,
  CURRENT_TIMESTAMP() as exec_timestamp
FROM landing l
LEFT JOIN warehouse w ON
  l.ad_id = w.ad_id AND
  l.date_id = w.date_id AND
  l.device = w.device AND
  l.conversion_tracker_id = w.conversion_tracker_id

post_operations {
    # Update older versions of the fact
    UPDATE ${ref("fact_ad_conversion_daily")} f
    SET 
    ad_conversion_type = t.l.ad_conversion_type,
    ad_conversion_name = t.l.ad_conversion_name,
    value = t.l.value,
    quantity = t.l.quantity
    FROM ${self()} t
    WHERE 
    f.ad_conversion_id = t.w.ad_conversion_id AND
    t.disposition = "MODIFY"
    ---
    # Insert the new versions of the fact
    INSERT INTO ${ref("fact_ad_conversion_daily")}
    SELECT 
    GENERATE_UUID() as ad_conversion_id,
    l.date_id as date_id,
    l.ad_id as ad_id,
    l.device as device,
    l.conversion_tracker_id as conversion_tracker_id,
    l.ad_conversion_type as ad_conversion_type,
    l.ad_conversion_name as ad_conversion_name,
    l.value as value,
    l.quantity as quantity
    FROM ${self()}
    WHERE disposition IN ("APPEND")
}